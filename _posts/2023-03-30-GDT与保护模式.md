---
layout:     post
title:      my-OS——bootGDT与保护模式
subtitle:   my-OS——bootGDT与保护模式
date:       2023-03-29
author:     xc
header-img: img/post-bg-ios9-web.jpg
catalog: true
tags:
    - OS
    - linux
---
## 从mbr到loader
上述已经将 `mbr` 加载到内存中，`mbr`将loader加载到内存的`LOADER_BASE_ADDR`基地址处，并且程序流通过mbr最后一条指令 

`jmp LOADER_BASE_ADDR`

执行到`loader`的入口处，执行loader程序

## Loader的工作
 - 建立 `GDT`也就是全局描述符表，又叫`段描述符表`,初始化内核代码和数据段寄存器，CPU进入`保护模式`，为内存段添加一些额外的安全属性，如特权级、段界限、段类型等。描述内存段属性的数据结构就叫`段描述符`。
 
```
 
 全局描述符表`GDT`（Global Descriptor Table）在整个系统中，
 全局描述符表GDT只有一张(一个处理器对应一个GDT)，GDT可以
 被放在内存的任何位置，但CPU必须知道GDT的入口，也就是基地
 址放在哪里，Intel的设计者门提供了一个寄存器GDTR用来存放GDT
 的入口地址，程序员将GDT设定在内存中某个位置之后，可以通过
 LGDT指令将GDT的入口地址装入此寄存器，从此以后，CPU就根据
 此寄存器中的内容作为GDT的入口来访问GDT了。GDTR中存放的
 是GDT在内存中的基地址和其表长界限。

```

 - 建立kernel 页目录和页表，打开虚拟内存，进入`paging`模式
 
 - 加载kernel 镜像到内存，进入kernel代码执行，至此系统的控制权转交给`kernel`

## 相关知识
### 实模式与保护模式
 - 实模式
 
 在这种状态下软件可访问的物理内存空间不超过`1M`（之前有提及）。实模式将整个物理内存看成`分段`的区域，程序代码和数据位于不同区域，操作系统和用户程序并没有区别对待，而且每一个指针都是指向实际的`物理地址`。这样，用户程序的一个指针如果指向了操作系统区域或其他用户程序区域，并修改内容，结果可能是灾难性的。
 
 - 保护模式
 
 在保护模式下，32根地址线全部有效，寻址依然由`cs:ip`也就是`代码段寄存器`与`指令指针寄存器`决定，而此时的寻址方式与实模式下有很大的不同。此时`cs`代码段寄存器（其中内容称`段选择子`）`GDT`中选择`段描述符`，而由`ip`指令寄存器决定偏移量，其中`ip`指令指针为32位，则寻址范围最多可以达到`4GB`
 
 ![1](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/059d9ad1058a4ffc9f49c5cb9d29aa4e~tplv-k3u1fbpfcp-zoom-in-crop-mark:4536:0:0:0.awebp  "1")
 
 可访问`64TB`=2^46（有2^14个段，每个段最大空间为2^32字节)，可采取分段存储管理机制和分页存储管理机制。
 保护模式下，有两个段表：GDT和LDT，每一张段表可以包含8192=2^13个段描述符，因而最多可以存在`2^14`个段。
 
 - 分段存储管理机制
 
 只有在保护模式下才能使用`分段存储管理机制`。分段机制将内存划分为起始地址和长度限制这两个二维参数表示的内存块，这些内存块就称之为段。
 
 分段机制涉及4个关键内容：`逻辑地址、段描述符、段描述符表、段选择子`。
 
 段描述符：每个段由如下三个参数定义：`段基地址`、`段界限`、`段属性`。
 
 ![3](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/a35aaedb3dd646e0a079abc66d256809~tplv-k3u1fbpfcp-zoom-in-crop-mark:4536:0:0:0.awebp "3")
 
 段描述符表：
 作为存放段描述符的容器，需要手动将其建立并通过lgdt指令加载到内存之中。其中描述符表最开始的16字节全为0。以下通过代码示范全局描述符表数据结构：

```

;=== 头64位全为0

gdt_start_desc  dq  0x0000000000000000

;=== 代码段描述符

gdt_code_desc   dq  0x00cf9a000000ffff

;=== 数据段描述符

gdt_data_desc   dq  0x00cf92000000ffff

;=== 描述符表限长

gdt_size	dw	$ - gdt_start_desc

;=== 描述符基址

gdt_base	dd	gdt_start_desc

```
通过`lgdt [gdt_size]`就可以将GDT基址指针对准我们刚刚创建的全局描述符表在内存中开始的地址了。

段选择子：

在保护模式中，cs代码段寄存器长度依然为16位，其中的内容称为段选择子，其数据结构为：

![2](https://p9-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/08a13602d84c498bbd45d9ccd9e5538c~tplv-k3u1fbpfcp-zoom-in-crop-mark:4536:0:0:0.awebp  "2")

### ELF文件格式

一个ELF文件由以下三部分组成：
 
 - ELF头-描述文件的主要特性：类型，CPU架构，入口地址，现有部分的大小和偏移等等；
 
 - 程序头表：列举了所有有效的段和他们的属性。程序头表需要加载器将文件中的节加载到虚拟内存段中
 
 - 节头表：包含对节的描述
 
## Loader实现

```
# 入口
loader_start

# 初始化 GDT 并进入保护模式
setup_protection_mode
protection_mode_entry

# 初始化 kernel 页目录和页表
setup_page

# 加载并进入 kernel
init_kernel
```
### 进入loader

定义loader编码的起始位置，我们这里定义起始内存地址为0x8000，mbr会将loader从磁盘上加载到内存0x8000位置处并跳转过去，所以Loader的编码地址必须
从该地址开始

```
; LOADER_BASE_ADDR = 0x8000
SECTION loader vstart=LOADER_BASE_ADDR
```

接下来正式进入loader的第一条代码`jmp loader_start`，它是一个简单的跳转

```
loader_entry:
  jmp loader_start

; 全局数据
; ...

loader_start:
  call clear_screen
  call setup_protection_mode
```
  
### 初始化GDT表
  
这里定义了4个GDT entry，每个entry占用8个字节即64bits。
GDT第一个entry是保留项不做使用；第四个为显示器video内存段描述符，这个其实并不是必须的，你可以无视它；所以我们只需要关注第二和第三项即可：
 
 - 内核代码段描述符；
 
 - 内核数据段描述符；
 
 我们用dd伪指令定义这两个段描述符，也就是定义双字类型变量，一个双字数据占4个字节单元，读完一个，偏移量加4
 
```
 CODE_DESC:
  dd DESC_CODE_LOW_32
  dd DESC_CODE_HIGH_32

DATA_DESC:
  dd DESC_DATA_LOW_32
  dd DESC_DATA_HIGH_32
```

### 进入保护模式

设置完GDT，我们可以进入保护模式

```
; enable A20
in al, 0x92
or al, 0000_0010b
out 0x92, al

; load GDT
lgdt [gdt_ptr]

; open protection mode - set cr0 bit 0
mov eax, cr0
or eax, 0x00000001
mov cr0, eax

; refresh pipeline
jmp dword SELECTOR_CODE:protection_mode_entry
```

这里使用了 lgdt 指令加载 GDT，并且打开了 cr0 寄存器的保护模式的 bit 位，置为1，正式进入保护模式。后面通过一个 far jump，将 cs 段寄存器初始化为 kernel code 段。注意 cs 寄存器的值不能直接通过 mov 指令设置，而是必须通过跳转语句隐式地被设置。

跳转后，接下来程序来到 protection_mode_entry 的执行，这里初始化了几个 kernel data 段寄存器：

```
protection_mode_entry:
  ; set data segments
  mov ax, SELECTOR_DATA
  mov ds, ax
  mov es, ax
  mov ss, ax

  ; set video segment
  mov ax, SELECTOR_VIDEO
  mov gs, ax
```

到此保护模式的初始化工作算是完成，然后就来到了 loader 的重点部分 setup_page 函数，开始建立 kernel 的虚拟内存，留待下一篇。

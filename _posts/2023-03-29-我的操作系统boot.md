---
layout:     post
title:      my-OS——boot
subtitle:   my-OS——boot
date:       2023-03-29
author:     xc
header-img: img/post-bg-ios9-web.jpg
catalog: true
tags:
    - OS
    - linux
---
#开机进入BIOS
计算机开始运行后将会运行硬盘中第一扇区的内容，第一扇区的大小为512B。

开机后CPU初始模式是实模式，cpu通过cs:ip，即代码段寄存器：指令指针寄存器来寻址，具体的寻址算法为物理地址=cs×10h+ip，其中cs代码段寄存器长度为16位，ip指令指针寄存器长度也为16位，则寻址范围在0x00000~0xfffff之间，地址宽度为20位，所以最大地址空间为1MB。这1MB的地址空间的划分是固定的，每一块都有规定的用途：
![](https://segmentfault.com/img/bVcSx6i)

##BIOS的工作
 - 开机后CPU的指令寄存器ip被强制置为地址0xFFFF0,这一地址被映射到BIOS固件上的代码
 - CPU开始执行BIOS上的代码，检查硬件输入输出设备，建立一个最初的中断向量表
  - 检查启动盘上的mbr分区，即磁盘上的第一个512B的内容，又叫引导分区；BIOS会对这512B做检查，最后两个字节必须是magic number:0x55和0xaa
   - BIOS将这512B加载到内存0x7c00处，到0x7e00为止，即
   0x0000 0111 1100 0000 0000到0x0000 0111 1110 0000 0000，然后指令跳转到0x7c00开始执行
   ![](https://segmentfault.com/img/bVcSzGy)
   图中标出了 BIOS 的主要工作流程，从地址 0xFFFF0 开始，经过一系列代码执行，最终校验并读取磁盘第一个 512B 扇区，加载到黄色部分即为 mbr，地址为 0x7C00，然后指令跳转过去，进入 mbr 的执行；
 
##mbr的工作
mbr的工作是将后面的loader部分从磁盘加载到内存，并跳转到loader继续执行。

MBR_BASE_ADDR 定义在了 boot.inc 中，为 0x7C00，这表示了整个 mbr 里的内容都是从 0x7C00 开始编址，包括代码和数据

```
; mbr
;------------------------------------------------------------
%include "src/boot/boot.inc"
//mbr放置在内存的起始地址
SECTION mbr vstart=MBR_BASE_ADDR

//调用初始化寄存器，加载load到内存，跳到load继续运行
mbr_entry:
  call init_segments
  call load_loader_img

  ; jump to loader
  jmp LOADER_BASE_ADDR

init_segments:
  mov ax, cs
  mov ds, ax
  mov es, ax
  mov ss, ax
  mov fs, ax

  ; let's move stack to 0x7b00 :)
  ; we need to copy the current return address to new stack base
  mov bx, sp
  mov ax, [bx]
  mov [0x7b00], ax
  mov sp, 0x7b00
  ret

load_loader_img:
  ; 28-bit LBA, so use 32-bit eax register
  mov eax, LOADER_START_SECTOR  ; 0x01
  mov bx, LOADER_BASE_ADDR  ; 0x8000
  mov cx, 0x08  ; loader max size 8 * 512 = 4KB
  call read_disk
  ret

read_disk:
  mov esi, eax
  mov di, cx

  ; sector count
  mov dx, 0x01f2
  mov al, cl
  out dx, al

  mov eax, esi

  ; LBA low
  mov dx, 0x1f3
  out dx, al

  ; LBA mid
  shr eax, 8
  mov dx, 0x1f4
  out dx, al

  ; LBA high
  shr eax, 8
  mov dx, 0x1f5
  out dx,al

  ; device reg: LBA[24:28]
  shr eax, 8
  and al, 0x0f

  or al, 0xe0  ; 0x1110, LBA mode
  mov dx, 0x1f6
  out dx, al

  ; command reg: 0x2 read, start reading
  mov dx, 0x1f7
  mov al, 0x20
  out dx, al

.not_ready:
  nop
  in al, dx
  and al, 0x88  ; bit 7 (busy), bit 3 (data ready)
  cmp al, 0x08
  jnz .not_ready

  ; di = cx = sector count
  ; read 2 bytes time, so loop (sector count) * 512 / 2 times
  mov ax, di
  mov dx, 256
  mul dx
  mov cx, ax

  mov dx, 0x1f0

.go_on_read:
  in ax, dx
  mov [bx], ax
  add bx, 2
  loop .go_on_read
  ret


times 510-($-$$) db 0
db 0x55, 0xaa
```

